load("@io_bazel_rules_docker//container:container.bzl", "container_image", "container_push")
load("//helm:defs.bzl", "helm_chart", "helm_lint_test")

exports_files(["Chart.lock"])

helm_chart(
    name = "with_image_deps",
    images = [
        ":image_a.push",
        ":image_b.push",
    ],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

helm_lint_test(
    name = "with_image_deps_test",
    chart = ":with_image_deps",
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

_IMAGES = [
    "image_a",
    "image_b",
]

[
    container_image(
        name = name,
        base = "@rules_helm_test_container_base//image",
        tags = ["manual"],
    )
    for name in _IMAGES
]

[
    container_push(
        name = "{}.push".format(name),
        format = "Docker",
        image = ":{}".format(name),
        registry = "docker.io",
        repository = "rules_helm/test/{}".format(name),
        tag = "latest",
        tags = ["manual"],
    )
    for name in _IMAGES
]
