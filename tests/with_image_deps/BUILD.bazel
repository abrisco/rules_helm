load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push")
load("//helm:defs.bzl", "helm_chart", "helm_lint_test")
load("//tests:test_defs.bzl", "helm_package_regex_test")

exports_files(["Chart.lock"])

EXCLUDE_WINDOWS = select({
    # TODO: rules_oci is broken on simple windows systems such as the windows github runners
    # https://github.com/abrisco/rules_helm/issues/53
    "@platforms//os:windows": ["@platforms//:incompatible"],
    "//conditions:default": [],
})

helm_chart(
    name = "with_image_deps",
    chart = "Chart.yaml",
    images = [
        ":image_a.push",
        "//tests/with_image_deps:image_b.push",
        "//tests/with_image_deps:image_c.push",
    ],
    target_compatible_with = EXCLUDE_WINDOWS,
    values = "values.yaml",
)

helm_lint_test(
    name = "with_image_deps_test",
    chart = ":with_image_deps",
    target_compatible_with = EXCLUDE_WINDOWS,
)

helm_package_regex_test(
    name = "with_image_deps_regex_test",
    package = ":with_image_deps",
    target_compatible_with = EXCLUDE_WINDOWS,
    values_patterns = [
        r"image_a:\s+url:\s+\"docker.io/rules_helm/test/image_a@sha256:[a-z0-9]{64}\"",
        r"image_b:\s+url:\s+\"docker.io/rules_helm/test/image_b@sha256:[a-z0-9]{64}\"",
        r"image_c:\s+url:\s+\"docker.io/rules_helm/test/image_c:1.2.3\"",
    ],
)

_IMAGES = [
    "image_a",
    "image_b",
    "image_c",
]

[
    oci_image(
        name = name,
        base = "@rules_helm_test_container_base",
        target_compatible_with = EXCLUDE_WINDOWS,
    )
    for name in _IMAGES
]

oci_push(
    name = "image_a.push",
    image = ":image_a",
    remote_tags = ["latest"],
    repository = "docker.io/rules_helm/test/image_a",
    target_compatible_with = EXCLUDE_WINDOWS,
)

write_file(
    name = "image_b.repository",
    out = "image_b.repository.txt",
    content = ["docker.io/rules_helm/test/image_b"],
    tags = ["manual"],
)

oci_push(
    name = "image_b.push",
    image = ":image_b",
    remote_tags = ["latest"],
    repository_file = ":image_b.repository.txt",
    target_compatible_with = EXCLUDE_WINDOWS,
)

oci_push(
    name = "image_c.push",
    image = ":image_c",
    remote_tags = ["1.2.3"],
    repository = "docker.io/rules_helm/test/image_c",
    target_compatible_with = EXCLUDE_WINDOWS,
)
